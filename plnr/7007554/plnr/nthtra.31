



;;;USES INDENT AND PPRIN FNS FROM GRIND
;;;
;;;SYSTEM FUNCTIONS SUCH AS THGOAL, THASSERT, THERASE AND THEOREM
;;;(ALL THMS) ARE TRACED IF THEY ARE ON 'THTRACE'.  THTRACES1 PUTS
;;;THEM THERE AND THUNTRACE TAKES THEM OFF. 
;;;THTRACE IS INITIALLY SET TO NIL BY TS PLNR

(OR (GETL 'GRINDEF '(FSUBR FEXPR))
    (PROG2 (FASLOAD F GRIND COM) (FASLOAD FORMAT FASL DSK PLNR)))

(DEFUN THTRACE FEXPR (L) (MAPC (FUNCTION THTRACE1) L))

(DEFUN THTRACE1 (X) 
       (PROG (Y) 
	     (AND (EQ X 'FAILING)
		  (SETQ TRACE-FAILING T INDENT 0.)
		  (RETURN X))
	     (SETQ X						       ;VARIETY OF POSSIBLE INPUT FORMATS TRANSFORMED
		   (COND ((ATOM X) (LIST X T NIL))		       ;TO STANDARD 3 ELEMENT LIST (OBJECT-TO-BE-TRACED
			 ((CDDR X) X)				       ;TRACE-CONDITION BREAK CONDITION)
			 ((NULL (CDR X)) (PRINT X)
					 (PRINC 'BAD/ FORMAT)
					 (RETURN NIL))
			 ((LIST (CAR X) (CADR X) NIL))))
	     (COND
	      ((GET (CAR X) 'THEOREM)				       ;IF OBJECT-TO-BE-TRACED IS A PARTICULAR THEOREM,
	       (COND ((SETQ Y (ASSQ 'THEOREM THTRACE))		       ;THEN THE TRIPLET '(THEOREM (THSEL 'CADR)(THSEL
		      (RPLACD Y					       ;'CADDDR)) IS GUARANTEED TO BE ON THTRACE IN
			      '((THSEL 'CADR) (THSEL 'CADDR))))	       ;ADDITION TO THE STANDARD TRIPLET
		     ((SETQ THTRACE
			    (LIST X
				  (APPEND '(THEOREM (THSEL 'CADR)
						    (THSEL 'CADDR))
					  THTRACE)))))))
	     (COND ((SETQ Y (ASSQ (CAR X) THTRACE))
		    (RPLACD Y (CDR X)))				       ;THTRACE IS UPDATED. IF THE OBJECT-TO-BE-TRACED
		   ((SETQ THTRACE (CONS X THTRACE))))		       ;IS ALREADY ON THTHRACE THEN THE TRACE AND BREAK
	     (RETURN X)))					       ;CONDITIONS ARE UPDATED. ELSE THE WHOLE TRIPLET
								       ;IS PLACED ON THTRACE

(DEFUN THUNTRACE FEXPR (L) 					       ;THUNTRACE REMOVES ELEMENTS OF ITS ARG FROM
       (PROG NIL 						       ;THTRACE IF NOT GIVEN ANY ARGS, THUNTRACE SETS
	     (AND (MEMQ 'FAILING L)				       ;THTRACE TO NIL
		  (OR (SETQ TRACE-FAILING
			    NIL
			    INDENT
			    0.
			    L
			    (DELETE 'FAILING L))
		      (RETURN '(FAILING))))
	     (COND
	      (L (SETQ THTRACE
		       (MAPCAN 
			(FUNCTION (LAMBDA (X) (COND ((MEMQ (CAR X) L)
						     (PRINT X)
						     NIL)
						    ((LIST X)))))
			THTRACE)))
	      ((MAPC (FUNCTION PRINT) THTRACE) (SETQ THTRACE NIL)))))



;;*PPAGE


;;;THTRACES IS ACTIVATED BY THGOAL, THASSERT, ... IF THTRACE IS NON-NIL
;;;THF IS SET TO THE PARTICULAR CANDIDATE FOR TRACEAGE, E.G.
;;;TO 'THGOAL IF THE PLANNER FUNCTION THGOAL ACTIVATED THTRACES
;;;THL = THE INSTANTIATED ARG OF THF. SEE DESC OF X ON NEXT PAGE

(DEFUN THTRACES (THF THL) 
       (PROG (THY THZ THB) 
	     (AND (SETQ THY (ASSQ THF THTRACE))			       ;THY SET TO TRIPLET ON THTRACE. IF NOT THERE, NO
		  (OR (SETQ THB (THVAL (CADDR THY) THALIST))	       ;TRACING IF BOTH TRACE AND BREAK ARE FALSE,
		      (THVAL (CADR THY) THALIST))		       ;DON'T TRACE SIDE EFFECT - THB SET TO VALUE OF
		  (OR (SETQ THZ (GET THF 'THTRACE))		       ;BREAK THZ IS SET TO THE TRACE FUNCTION FOR THE
		      (THERT THTRACES - TRACE LOSSAG))		       ;OBJECT-TO-BE-TRACED
		  (THZ THL THB)					       ;THE TRACE FN IS EXECUTED
		  THB						       ;IF THB IS NON-NIL, BREAK
		  (THERT))))



;;;THE CAR OF THE TREE IS 
;;; '(THTRACES NAME-OF-TRACE-POINT OPTIONAL-PRINT-OF-THVALUE (THERT)-OR-NIL)
;;;THUS, THESE TWO FNS PRINT THE NAME OF THE TRACE POINT, "FAIL"-OR-"SUCCEED"
;;;PRINT THVALUE IF ANY, AND FINALLY BREAK IF (THERT) IS PRESENT, 
;;;THEN POP THE TREE

(DEFPROP THTRACES
	 (LAMBDA NIL (TINDENT)
		     (PRINC (CADAR THTREE))
		     (PRINC '/ FAILED/ )
		     (EVLIS (CDDAR THTREE))
		     (AND TRACE-FAILING
			  (GET (CADAR THTREE) 'THEOREM)
			  (POPINDENT))
		     (THPOPT)
		     NIL)
	 THFAIL)

(DEFPROP
 THTRACES
 (LAMBDA NIL 
  (TINDENT)
  (PRINC (CADAR THTREE))
  (PRINC '/ SUCCEEDED/ )
  (EVLIS (CDDAR THTREE))
  (AND TRACE-FAILING
       (GET (CADAR THTREE) 'THEOREM)
       (THPUSH THBRANCH
	       (LIST 'THFAIL?
		     T
		     (LIST 'PROG
			   NIL
			   '(TINDENT)
			   '(PRINC 'FAILING/ BACK/ INTO/ THEOREM/ )
			   (LIST 'PRINC
				 (LIST 'QUOTE (CADAR THTREE)))
			   '(PUSHINDENT)
			   '(RETURN NIL))))
       (POPINDENT))
  (THPOPT)
  THVALUE)
 THSUCCEED)

;;;THE TRACE FNS THBKPT, THGOAL, THEOREM, THASSERT, AND THERASE PUSH ONTO THE TREE
;;;'(THTRACES NAME-OF-TRACE-POINT OPTIONAL-PRINT-OF-THVALUE (THERT)-OR-NIL)
;;;X=THL=INSTANTIATED GOAL, ASSERTION OR ERASURE, NAME OF THE THM OR 
;;;MESSAGE OF THE BREAKPOINT

(DEFPROP THBKPT
	 (LAMBDA (X B) (THPUSH THTREE
			       (LIST 'THTRACES
				     (THGENS B)
				     (AND B '(THERT))))
		       (TINDENT)
		       (PRINC 'PASSING/ BKPT/ )
		       (PRIN1 (CADAR THTREE))
		       (PRINC '/ )
		       (SETQ THBRANCH THTREE)			       ;BY SETTING THBRANCH AND THABRANCH, A TRIPLE IS
		       (SETQ THABRANCH THALIST)			       ;CREATED BY THVAL FOR BACKTRACKING.  THEN, THE
		       (THPOPT)					       ;TREE IS POPPED TO PREVENT THTRACES FROM TYPING
		       (PRIN1 X))				       ;OUT THE MEANINGLESS THAT THE BREAKPOINT
	 THTRACE)						       ;SUCCEEDED.

(DEFPROP THGOAL
	 (LAMBDA (X B) (THPUSH THTREE
			       (LIST 'THTRACES
				     (THGENS G)
				     '(AND THVALUE (PRIN1 THVALUE))
				     (AND B '(THERT))))
		       (TINDENT)
		       (PRINC 'TRYING/ GOAL/ )
		       (PRIN1 (CADAR THTREE))
		       (PRINC '/ )
		       ((LAMBDA (M) (PPRIN X 'LINE)) 0.))
	 THTRACE)

(DEFPROP THEOREM
	 (LAMBDA (X B) 
		 (THPUSH THTREE
			 (LIST 'THTRACES
			       X
			       '(AND THVALUE (PRIN1 THVALUE))
			       (AND B '(THERT))))
		 (TERPRI)
		 (TINDENT)
		 (PRINC 'ENTERING/ THEOREM/ )
		 (PRIN1 X)
		 (TINDENT)
		 (PRINC 'FOR/ )
		 ((LAMBDA (M) (PPRIN (CALLING-PATTERN) 'LINE))
		  0.)
		 (AND TRACE-FAILING (PUSHINDENT)))
	 THTRACE)

(DEFPROP THASSERT
	 (LAMBDA (X B) (THPUSH THTREE
			       (LIST 'THTRACES
				     (THGENS A)
				     (AND B '(THERT))))
		       (TINDENT)
		       (PRINC 'ASSERTING/ )
		       (PRIN1 (CADAR THTREE))
		       (PRINC '/ )
		       (PRIN1 X))
	 THTRACE)

(DEFPROP THERASE
	 (LAMBDA (X B) (THPUSH THTREE
			       (LIST 'THTRACES
				     (THGENS E)
				     (AND B '(THERT))))
		       (TINDENT)
		       (PRINC 'ERASING/ )
		       (PRIN1 (CADAR THTREE))
		       (PRINC '/ )
		       (PRIN1 X))
	 THTRACE)

T

;;;UTILITY FNS
;;;FOR THE TRACE-OBJECT 'THEOREM, IF ANY SPECIFIC THMS ARE TRACED,
;;;	'(THSEL 'CADR) AND '(THSEL 'CADDDR)
;;;ARE THE TRACE AND BREAK PREDICATES.  HENCE THTRACES CAUSES
;;;THESE EXPR'S TO BE THVALED. THL IS SET TO THE SECOND ARG
;;;OF THTRACES WHICH IN THIS CASE IS PRESUMABLY THE NAME OF 
;;;THE PARTICULAR THM THAT ACTIVATED THTRACES. THSEL FIRST
;;;CHECKS TO SEE WHETHER THIS THM IS INDEPENDENTLY ON THTRACE. 
;;;IF NOT, IT DOES NO MORE. BUT IF IT IS, THX GETS SET TO THE THM'S 
;;;TRIPLET. THEN THX GETS SET TO EITHER THE TRACE (ARG='CADR) OR THE BREAK 
;;;(ARG='CADDDR) CONDITION OF THE TRIPLET. FINALLY, THESE CONDITIONS ARE THVALED
;;;THUS, THSEL SERVES THE PURPOSE OF REFERENCING THE TRACE AND BREAK
;;;PREDICATES OF PARTICULAR THMS ON THTRACE

(DEFUN THSEL (THF) 
       (PROG (THX) 
	     (RETURN (AND (SETQ THX (ASSQ THL THTRACE))
			  (SETQ THX (THF THX))
			  (THVAL THX THALIST)))))

(DEFUN THGENS FEXPR (X) 					       ;MAKES A NAME WITH PREFIX X AND SUFFIX A UNIQUE
       (MAKNAM (NCONC (EXPLODE (CAR X))				       ;NUMBER
		      (EXPLODE (SETQ THGENS (ADD1 THGENS))))))

(SETQ THGENS 0.)

(DEFUN CALLING-PATTERN NIL 					       ;RETURNS CALLING PATTERN OF THEOREM
       ((LAMBDA (PATTERN) (COND ((EQ (CAAR PATTERN) 'THAPPLY)
				 (CADDAR PATTERN))
				(PATTERN)))
	(CADR (CAR (CDDDR THTREE)))))

(DEFUN POPINDENT NIL (SETQ INDENT (*DIF INDENT TAB)))

(DEFUN PUSHINDENT NIL (SETQ INDENT (PLUS INDENT TAB)))

(DEFUN TINDENT NIL 
       (TERPRI)
       (DO ((I (*QUO INDENT WRAPAROUND) (/1- I)))
	   ((= I 0.))
	   (PRINC '_))
       (INDENT (REMAINDER INDENT WRAPAROUND)))

(SETQ TRACE-FAILING NIL INDENT 0. TAB 5. WRAPAROUND 80.)



;-*- MODE: LISP; PACKAGE:USER; BASE: 8-*-

(DEFUN DOLLAR-READ-MACRO (IGNORE STREAM)
  (LET ((CH (FUNCALL STREAM ':TYI)))
    (COND ((= CH #/?)
	   `(VAR ,(READ STREAM)))
	  (T (FERROR NIL "bad char after $")))))

;(DEFVAR FILE-READTABLE (COPY-READTABLE SI:INITIAL-READTABLE))

(SET-SYNTAX-MACRO-CHAR #/$ 'DOLLAR-READ-MACRO)

(DEFVAR TWO-ELEMENT-ASSERTIONS-LIST NIL)

(DEFVAR TWO-ELEMENT-CONSEQUENTS-LIST NIL)

(DEFMACRO CURRENT-BINDING-LIST (VL)
  `(COMPILER:%BINDING-INSTANCES ,VL))

(DEFMACRO IVC (X)
  `(COMPILER:%INTERNAL-VALUE-CELL ,X))

(DEFUN CALL-CONTINUATION (CONT ENV)
  (COMPILER:%USING-BINDING-INSTANCES ENV)
  (FUNCALL CONT ENV))

(DEFUN SETUP NIL
  (thasst-2 'thconse-2-a)   ;assert (human x) -> (fallible x)
  (thasst-2 'thconse-2-b)   ;assert (greek x) -> (human x)
  (thass-2 'greek 'socrates))

(DEFVAR THPROG-CATCH-TAG)
(DEFVAR THPROG-TAG-ALIST)

(DEFMACRO THPROG (VARS &REST BODY &AUX)
 `(LOCAL-DECLARE ((SPECIAL ,@VARS))
    (LET ,(MAPCAR (FUNCTION (LAMBDA (X) `(,X 'UNASSIGNED))) VARS)
      ,(EXPAND-THPROG VARS BODY))))

(DEFMACRO THFINDALL (VAR &REST BODY)
  `(LOCAL-DECLARE ((SPECIAL ANS ,VAR))
     (LET ((ANS NIL) (,VAR 'UNASSIGNED))
       ,(EXPAND-PATTERN-BODY `(CURRENT-BINDING-LIST '(ANS ,VAR))
			     (APPEND BODY `((THFINDALL-COLLECT ,VAR))))
       ANS)))

(DEFUN EXPAND-PATTERN-BODY (ENV BODY)
  (IF (NULL BODY) NIL
      (EXPAND-PATTERN-BODY-STEP ENV (CAR BODY) (CDR BODY))))

(DEFUN EXPAND-PATTERN-BODY-STEP (ENV STEP CONT)
  (LET ((EXPAND-FN (GET (CAR STEP) 'PATTERN-EXPANDER)))
    (IF (NULL EXPAND-FN) (FERROR NIL "~S has no expander" STEP)
	(FUNCALL EXPAND-FN ENV STEP CONT))))

(DEFUN EXPAND-CONTINUATION (CONT)
  (IF (SYMBOLP CONT) CONT
      `(FUNCTION (LAMBDA (E)
		   ,(EXPAND-PATTERN-BODY 'E CONT)))))

(DEFUN EXPAND-THPROG (VARS BODY)
  (LET ((THPROG-CATCH-TAG (GENSYM))
	(THPROG-TAG-ALIST (MAKE-THPROG-ALIST BODY)))
    `(*CATCH ',THPROG-CATCH-TAG
       ,(EXPAND-PATTERN-BODY `(CURRENT-BINDING-LIST ',VARS) BODY))))

(DEFUN MAKE-THPROG-ALIST (BODY)
  (LET ((ANS NIL))
    (DOLIST (E BODY)
      (IF (ATOM BODY)
	  (PUSH (CONS E (GENSYM)) ANS)))
    ANS))

(DEFUN (THGOAL PATTERN-EXPANDER) (ENV STEP CONT)
  `(LOOP-THGOAL-2 ,ENV
	     ,(EXPAND-PATTERN-ELEMENT (FIRST (SECOND STEP)))
	     ,(EXPAND-PATTERN-ELEMENT (SECOND (SECOND STEP)))
	     ,(EXPAND-CONTINUATION CONT)))

(DEFUN (THAMONG PATTERN-EXPANDER) (ENV STEP CONT)
  `(LOOP-THAMONG ,ENV
    ,(EXPAND-PATTERN-ELEMENT (SECOND STEP))
    ,(THIRD STEP)
    ,(EXPAND-CONTINUATION CONT)))

(DEFUN EXPAND-PATTERN-ELEMENT (E)
  (COND ((ATOM E) `',E)
	((EQ (CAR E) 'VAR)
	 `(IVC ',(CADR E)))
	(T (FERROR NIL "Cant expand pattern element ~s" E))))

(DEFUN (THRETURN PATTERN-EXPANDER) (ENV STEP CONT) ENV CONT
  `(*THROW ',THPROG-CATCH-TAG ,(EXPAND-PATTERN-ELEMENT (CADR STEP))))

(DEFUN (THFINDALL-COLLECT PATTERN-EXPANDER) (ENV STEP CONT) ENV CONT
       `(SETQ ANS (CONS ,(CADR STEP) ANS)))

(DEFUN MTRY NIL
  (THPROG (X)
	  (THGOAL (FALLIBLE $?X))
	  (THGOAL (GREEK $?X))
	  (THRETURN $?X)))

(DEFUN TRY NIL 
  (LOCAL-DECLARE ((SPECIAL X))
    (LET ((X 'UNASSIGNED))
      (*CATCH 'G00XY
	(LOOP-THGOAL-2 (CURRENT-BINDING-LIST '(X))
		  'FALLIBLE (ivc 'X)
		  (FUNCTION (LAMBDA (E)
			      (LOOP-THGOAL-2 E
					'GREEK (ivc 'X)
					(FUNCTION (LAMBDA (E) E
							  (*THROW 'G00XY X) ))))))))))

(DEFUN MTRY1 NIL
  (THFINDALL X (THGOAL (FALLIBLE $?X))))

(DEFUN TRY1 NIL 
  (LOCAL-DECLARE ((SPECIAL X ANS))
    (LET ((X 'UNASSIGNED)
	  (ANS NIL))
      (LOOP-THGOAL-2 (CURRENT-BINDING-LIST '(X ANS))
		'FALLIBLE (ivc 'X)
		(FUNCTION (LAMBDA (E) E
				  (SETQ ANS (CONS X ANS)))))
      ANS)))

;(THCONSE (X) (FALLIBLE $?X)
;  (THGOAL (HUMAN $?X)))
(LOCAL-DECLARE ((SPECIAL P1 P2))
(DEFUN THCONSE-2-A (VAR-ENV CONT P1 P2)
   (COND ((EQ P1 'FALLIBLE)
	  (LOOP-THGOAL-2 VAR-ENV 'HUMAN (ivc 'P2) CONT)))))

;(THCONSE (X) (HUMAN $?X)
;  (THGOAL (GREEK $?X)))
(LOCAL-DECLARE ((SPECIAL P1 P2))
(DEFUN THCONSE-2-B (VAR-ENV CONT P1 P2) 
   (COND ((EQ P1 'HUMAN)
	  (LOOP-THGOAL-2 VAR-ENV 'GREEK (ivc 'P2) CONT)))))

(DEFUN THASS-2 (L1 L2 &AUX ASS)   ;SIMPLE ASSERTION, TWO LONG
   (SETQ ASS (LIST L1 L2))
   (COND ((NOT (MEMBER ASS TWO-ELEMENT-ASSERTIONS-LIST))
	  (SETQ TWO-ELEMENT-ASSERTIONS-LIST 
		(CONS ASS TWO-ELEMENT-ASSERTIONS-LIST)))))

(DEFUN THASST-2 (TH)		;ASSERT CONSEQUENT THEOREM, TWO LONG
   (COND ((NOT (MEMQ TH TWO-ELEMENT-CONSEQUENTS-LIST))
	  (SETQ TWO-ELEMENT-CONSEQUENTS-LIST 
		(CONS TH TWO-ELEMENT-CONSEQUENTS-LIST)))))

(LOCAL-DECLARE ((SPECIAL E1 E2))
(DEFUN LOOP-THGOAL-2 (VAR-ENV E1 E2 CONT)    ;ACHIEVE GOAL, TWO LONG
  (FORMAT T "~%THGOAL: ~s (dtp ~s) ~s (dtp ~s) "
	  E1 (%DATA-TYPE (ivc 'E1))
	  E2 (%DATA-TYPE (ivc 'E2)))
   (DOLIST (AS TWO-ELEMENT-ASSERTIONS-LIST)
       (COND ((EQ E1 'UNASSIGNED)
	      (SETQ E1 (FIRST AS))
	      (COND ((EQ E2 'UNASSIGNED)
		     (SETQ E2 (SECOND AS))
		     (CALL-CONTINUATION CONT VAR-ENV)
		     (SETQ E2 'UNASSIGNED))
		    ((EQ E2 (SECOND AS))
		     (CALL-CONTINUATION CONT VAR-ENV)))
	      (SETQ E1 'UNASSIGNED))
	     ((EQ E1 (FIRST AS))
	      (COND ((EQ E2 'UNASSIGNED)
		     (SETQ E2 (SECOND AS))
		     (CALL-CONTINUATION CONT VAR-ENV)
		     (SETQ E2 'UNASSIGNED))
		    ((EQ E2 (SECOND AS))
		     (CALL-CONTINUATION CONT VAR-ENV))))))
   (DOLIST (TH TWO-ELEMENT-CONSEQUENTS-LIST)
     ;(FORMAT T "~%Trying consequent theorm (~s ~s ~s)" TH E1 E2)
     (FUNCALL TH VAR-ENV CONT (ivc 'E1) (ivc 'E2)))))



(LOCAL-DECLARE ((SPECIAL V))
(DEFUN LOOP-THAMONG (VAR-ENV V LIST CONT)    ;ACHIEVE GOAL, TWO LONG
  (FORMAT T "~%THAMONG: ~S (DTP ~S) ~S" V (%DATA-TYPE (IVC 'V)) LIST)
  (COND ((NEQ V 'UNASSIGNED)
	 (COND ((MEMBER V LIST)
		(CALL-CONTINUATION CONT VAR-ENV))))
	(T (DOLIST (L-E LIST)
	     (SETQ V L-E)
	     (CALL-CONTINUATION CONT VAR-ENV))
	   (SETQ V 'UNASSIGNED)))))
